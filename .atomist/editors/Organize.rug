@tag "elm"
@description "make a bare elm program into a workable project"
editor Organize

AddGitIgnore
IndexHtml
RenameModuleToMain
MoveElmToSrc
AddBuildScript

editor AddGitIgnore
with project
  do addFile name=".gitignore" content={ "elm-stuff\ntarget" }

editor AddBuildScript
with project
  do copyEditorBackingFileOrFail sourcePath="build"

editor RenameModuleToMain
with project
  with file f
    with elm.module m when exposes("main") begin
      do rename "Main"
    end

editor MoveElmToSrc
let src_location = <
    ROOT : '"."';
    src_location : '"source-directories":' '[' here=ROOT;
>

with project begin
  with file e when nameContains ".elm" and { -1 === e.path().indexOf("elm-stuff") } begin
    do e.setPath newPath={ "src/" + e.name() }
  end
  with file when path = "elm-package.json"
    with src_location
      do set 'here' '"src"'
end

editor IndexHtml

let page_title=$({elm.module}$[.exposes('main')]).name

with project begin
  do eval { print("page title " + page_title); }
  do deleteFile "index.html"
  do merge "index.html.vm" to "resources/index.html"
  do merge "styles.css.vm" to "resources/styles.css"
end
