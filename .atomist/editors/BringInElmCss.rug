@tag "elm"
@description "Generate CSS from Elm code"
editor BringInElmCss

with project p
  begin
    do eval { print("\n*** Don't forget to npm install -g elm-css ! ***\n")}
    do merge 'MainCss.elm.vm' to 'src/MainCss.elm'
    do merge 'Stylesheets.elm.vm' to 'src/Stylesheets.elm'
  end

AddDependency dependency_name="rtfeldman/elm-css", dependency_version="5.0.0 <= v < 6.0.0"
AddLinkToHeader stylesheet="elm-styles.css"
AddCssBuildStep

@description "Add a stylesheet link to the header of index.html"
editor AddLinkToHeader
  param stylesheet:.*

  with file f when name = "index.html"
     do regexpReplace
      "</head>"
      { '        <link type="text/css" href="' + stylesheet + '" rel="stylesheet"/>\n</head>' }

@description "Create the CSS before compiling the Elm"
editor AddCssBuildStep

  let buildGrammar = <
      SOMETHING : 'target';
      PLACEHOLDER : [];
      expr : before=SOMETHING insert=PLACEHOLDER 'echo "Compiling..."';
  >

  with file f when name = "build"
    with buildGrammar
      with insert
        do setValue {
          return "echo 'Building css...'\nelm-css -o target src/Stylesheets.elm\n\n" }
